import com.diffplug.spotless.LineEnding

plugins {
    id "java"
    id "java-gradle-plugin"
    id "groovy"
    id "maven-publish"
    id "com.diffplug.spotless" version "6.25.0"
    id "com.gradleup.shadow" version "8.3.3"
}

repositories {
    mavenLocal()
    mavenCentral()
    gradlePluginPortal()
    maven { url 'https://jitpack.io' }
}

java {
    withSourcesJar()

    // Sets the Java version
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

dependencies {
    // Gradle API
    shadow(gradleApi())

    // ASM
    shadow("org.ow2.asm:asm:${asm_version}")
    shadow("org.ow2.asm:asm-util:${asm_version}")       { transitive = false }
    shadow("org.ow2.asm:asm-tree:${asm_version}")       { transitive = false }
    shadow("org.ow2.asm:asm-analysis:${asm_version}")   { transitive = false }
    shadow("org.ow2.asm:asm-commons:${asm_version}")    { transitive = false }

    // Access Manipulators
    implementation("com.github.PuzzleLoader:access_manipulators:${access_manipulators_version}")

    shadow("com.github.johnrengelman:shadow:8.1.1")

    implementation("org.apache.maven:maven-artifact:3.6.3")
}


gradlePlugin {
    plugins {
        puzzleGradle {
            id = "cr_puzzle_gradle"
            implementationClass = "com.github.puzzle.CosmicPuzzlePlugin"
        }
    }
}


spotless {
    lineEndings = LineEnding.UNIX

    java {
        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }
}


jar {
    dependsOn spotlessApply // Apply spotless so stuff is consistent

    from "license.txt"
    manifest {
        attributes(
                "Specification-Title":   id,
                "Specification-Vendor":  project.group,
                "Specification-Version": project.version,
        )
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = group
            artifactId = id

            artifact source: shadowJar, classifier: '', extension: 'jar'
        }
    }
}

wrapper {
    gradleVersion = "8.5"
    distributionType = Wrapper.DistributionType.ALL
}
